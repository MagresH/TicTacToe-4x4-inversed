plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.1.0'

}

tasks.named('test') {
    useJUnitPlatform()
}
application {
    mainClassName = 'com.example.tictactoe4x4inversed.TicTacToe4x4InversedApplication'
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}
def javaHome = System.getenv('JAVA_HOME') ?: System.getProperty('java.home')

task jlink(type: Exec) {
    dependsOn shadowJar

    def outputDir = file("$buildDir/jlink-image")
    def jreModules = 'java.base,java.logging,java.naming,java.desktop,java.management,java.security.jgss,java.instrument,java.sql'

    commandLine "${javaHome}/bin/jlink", '--no-header-files', '--no-man-pages', '--compress=2', '--strip-debug', '--add-modules', jreModules, '--output', outputDir
}

jlink.doLast {
    copy {
        from file("$buildDir/libs/${project.name}-${project.version}-all.jar")
        into file("$buildDir/jlink-image")
        rename { 'app.jar' }
    }
}

